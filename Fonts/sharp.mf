% A factor that determines the space between the vertical
% bars and the outer edge of the character as a fraction of
% the staff line distance
global_variable(numeric)(sharp_edge_distance_multiplier)(0.2);

% A factor that determines the height of the thin part as a
% fraction of the staff line distance
global_variable(numeric)(sharp_height_multiplier)(2.5);

% A factor that determines the width of the hole as a fraction of the
% staff line distance.
global_variable(numeric)(sharp_hole_width_multiplier)(0.33);

begingroup

  local_variable(numeric)(hole_width)
    (round(sharp_hole_width_multiplier * staff_line_distance));

  % Hope that half a pixel will not be visible and will not influence 
  % the required distance to the noteheads. 
  if(odd(hole_width)):
    local_variable(numeric)(xoffset)(0.5);
  else:
    local_variable(numeric)(xoffset)(0);
  fi;

  local_variable(numeric)(edge_distance)
    (sharp_edge_distance_multiplier * staff_line_distance);

  local_variable(numeric)(width)
    (hole_width + 2 * stem_thickness + 2 * edge_distance);

  local_variable(numeric)(xleft)(-0.5 * width);
  local_variable(numeric)(xright)(-xleft);
  local_variable(numeric)(yleft)(-0.15 * width);
  local_variable(numeric)(yright)(-yleft);

  % The path for the thick part symmetric around (0, 0)
  local_variable(path)(thickpart)
    ((xleft, yleft) -- (xright, yright));

  % Determine the y coordinate of the previous path at the
  % cross point of the thin part.  Use congruent triangles.
  local_variable(numeric)(ythin)
    ((xright - edge_distance) * yright / xright);

  local_variable(numeric)(height)
    (round(sharp_height_multiplier * staff_line_distance));

  % The path for the thin part symmetric around (0, 0)
  local_variable(path)(thinpart)
    ((0, 0.5 * height) -- (0, -0.5 * height));

  begin_character(sharp)

    % Draw the thick part
    pickup penrazor scaled (0.4 * staff_line_distance) rotated 90
                    scaled magnification;
    draw thickpart 
      shifted (0, 0.5 * staff_line_distance)
      shifted (xoffset, yoffset)
      scaled magnification;
    draw thickpart
      shifted (0, -0.5 * staff_line_distance)
      shifted (xoffset, yoffset)
      scaled magnification;
    % Draw the thin part
    pickup penrazor scaled stem_thickness
                    scaled magnification;
    draw thinpart 
      shifted (-0.5 * hole_width - 0.5 * stem_thickness, -ythin)
      shifted (xoffset, yoffset)
      scaled magnification;
    draw thinpart
      shifted (0.5 * hole_width + 0.5 * stem_thickness, ythin)
      shifted (xoffset, yoffset)
      scaled magnification;

  end_character;

  begin_character(sharp_light)

    % Draw the thick part
    pickup penrazor scaled (0.4 * staff_line_distance) rotated 90
                    scaled magnification;
    draw thickpart 
      shifted (0, 0.5 * staff_line_distance)
      shifted (xoffset, yoffset)
      scaled magnification;
    draw thickpart
      shifted (0, -0.5 * staff_line_distance)
      shifted (xoffset, yoffset)
      scaled magnification;
    % Draw the thin part
    pickup penrazor scaled stem_thickness
                    scaled magnification;
    draw thinpart 
      shifted (-0.5 * hole_width - 0.5 * stem_thickness, -ythin)
      shifted (xoffset, yoffset)
      scaled magnification;
    draw thinpart
      shifted (0.5 * hole_width + 0.5 * stem_thickness, ythin)
      shifted (xoffset, yoffset)
      scaled magnification;

    stripes(1,2);
  end_character;

endgroup;
