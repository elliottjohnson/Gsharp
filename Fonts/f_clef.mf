%                                                                       
%                                                                       
%                  i                                                    
%                  |                                                    
%***********************************************************************
%             *********                 |xj                             
%           **  |      **              ***                              
%          **   e       ***           *****_yj                          
%         **             ***          *****                             
%        *** |d           ***          ***                              
%  (0,0)\** ***            ***                                          
%***********************************************************************
%        *********          ***                                         
%      a-*********-c        ****       ***                              
%        *********          ****      *****_yk                         
%         *******         f-****-h    *****                            
%           ***             ****       ***                             
%            |b             ****                                        
%***********************************************************************
%                           ****                                        
%                          ****                                         
%                         ****                                          
%                        ****                                           
%                       ***                                             
%                     ***                                               
%***********************************************************************
%                  ***                                                  
%                ***                                                    
%              ***                                                      
%           ***                                                         
%      g-***                                                            
%        |                                                              
%         g + (0, -1)                                                   
%***********************************************************************
%                                                                       
%                                                                       
%                                                                       

begingroup

  local_variable(numeric)(bigdot_diameter)
    (staff_line_distance);

  local_variable(numeric)(yd)(round(0.2 * staff_line_distance));

  local_variable(numeric)(xa)(0);
  local_variable(numeric)(ya)(yd - 0.5 * bigdot_diameter);

  local_variable(numeric)(xb)(xa + 0.5 * bigdot_diameter);
  local_variable(numeric)(yb)(yd - bigdot_diameter);

  local_variable(numeric)(xc)(xa + bigdot_diameter);
  local_variable(numeric)(yc)(ya);

  local_variable(numeric)(xd)(xb);

  local_variable(numeric)(xe)(0.85 * staff_line_distance);
  local_variable(numeric)(ye)
    (staff_line_distance - 2 * staff_line_thickness);

  local_variable(numeric)(xf)(round(1.5 * staff_line_distance));
  local_variable(numeric)(yf)(-0.5 * staff_line_distance);

  local_variable(numeric)(xg)(0);
  local_variable(numeric)(yg)(-2.5 * staff_line_distance);

  local_variable(numeric)(xh)(xf + round(0.5 * staff_line_distance));
  local_variable(numeric)(yh)(yf);

  local_variable(numeric)(xi)(staff_line_distance);
  local_variable(numeric)(yi)(staff_line_distance);

  local_variable(numeric)(xj)
    (xh + max(1, round(0.2 * staff_line_distance)) + 0.5 * dot_diameter);
  local_variable(numeric)(yj)
    (round(0.5 * (staff_line_distance - staff_line_thickness)));

  local_variable(numeric)(yk)(-staff_line_thickness - yj);

  local_variable(path)(stuff)
    ((xa, ya){down} .. 
     (xb, yb){right} .. 
     (xc, yc){up} .. 
     (xd, yd){left} &
     (xd, yd) .. 
     (xe, ye){right} .. 
     (xf, yf){down} .. 
     (xg, yg) &
     (xg, yg) -- 
     (xg, yg - 1) &
     (xg, yg - 1) .. 
     (xh, yh){up} .. 
     (xi, yi){left} .. cycle);

   begin_character(f_clef)

      fill stuff shifted (0,staff_line_thickness) scaled magnification;
      fill fullcircle scaled dot_diameter 
                      shifted (xj, yj)
		      shifted (0,staff_line_thickness) 
                      scaled magnification;
      fill fullcircle scaled dot_diameter 
                      shifted (xj, yk)
		      shifted (0,staff_line_thickness) 
                      scaled magnification;

   end_character;
     

   begin_character(f_clef_light)

      fill stuff shifted (0,staff_line_thickness) scaled magnification;
      fill fullcircle scaled dot_diameter 
                      shifted (xj, yj)
		      shifted (0,staff_line_thickness) 
                      scaled magnification;
      fill fullcircle scaled dot_diameter 
                      shifted (xj, yk)
		      shifted (0,staff_line_thickness) 
                      scaled magnification;
      stripes(3,3);

   end_character;
     

endgroup;
