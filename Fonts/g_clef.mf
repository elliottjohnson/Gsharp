%                                 w
%                                 |
%                                 **
%                                ****
%                               ***** 
%                              ** | **
%                              ** h **
%                             **     **
%                            **    g-**
%                         v-**-i     **
%                           **       ***
%                           *        ***
%                           *        ***-x
%                           *       ****
%                            *      ****
%                            *      ****
%*************************************************************************
%                            *    ******
%                            *   ******
%                             * ******
%                             ******
%                           ******
%                     ee\ ******/dd
%*************************************************************************
%                       ********
%                      *******  *
%                     *******   *
%                    *******    *
%                   *******     *
%                   ******   ff\*/gg/c
%*************************************************************************
%                  ******     ************
%                 *****     ****************
%              f *****     ******************
%               \*****    *****  *  |      ***
%                ****  b-****    *  bb      **
%        (0, 0)\ ****/y  ***/cc  *       aa\**/d
%*************************************************************************
%                ****     **      *         **
%                 ***      *      *        **
%                  **      |      *       **
%                   **     a  z ) *      **
%                    ***      |    *   **
%                       ***************
%*************************************************************************
%                             |e   *
%                         o        *
%                         |         *
%                        ***        *
%                      *******      *
%                     *********     *
%                   n-*********-p   *
%                      ******     s-*-l
%                       ****-q r   *
%                        **** /  **
%                          *******
%                             |
%                             m
%
%

begingroup

  local_variable(numeric)(xf)(0);
  local_variable(numeric)(yf)(0.5 * staff_line_distance);


  local_variable(numeric)(xy)
    (max(2, round(0.4 * staff_line_distance)));
  local_variable(numeric)(yy)
    (0.2 * staff_line_distance);

  local_variable(numeric)(xb)
    (xy + max(2, round(0.4 * staff_line_distance)));
  local_variable(numeric)(yb)
    (0.3 * staff_line_distance);

  local_variable(numeric)(xcc)
    (xb + max(2, round(0.4 * staff_line_distance)));
  local_variable(numeric)(ycc)(0);

  local_variable(numeric)(xa)
    (xcc + max(1, 0.2 * staff_line_distance));
  local_variable(numeric)(ya) 
    (-0.4 * staff_line_distance);

  local_variable(numeric)(xc) 
    (xb + round(0.7 * staff_line_distance));
  local_variable(numeric)(yc)
    (staff_line_distance 
     + max(1, 0.15 * staff_line_distance));

  local_variable(numeric)(xd)
    (xc + staff_line_distance);
  local_variable(numeric)(yd)(0);


  local_variable(numeric)(xe)
    (1.5 * staff_line_distance);
  local_variable(numeric)(ye)
    (-staff_line_thickness - staff_line_distance);

  local_variable(numeric)(xg)
    (round(1.8 * staff_line_distance));
  local_variable(numeric)(yg)
    (3.8 * staff_line_distance);

  local_variable(numeric)(xw)
    (xg - 2 * staff_line_thickness);
  local_variable(numeric)(yw)
    (round(5.0 * staff_line_distance));

  local_variable(numeric)(xh)
    (xw);
  local_variable(numeric)(yh)
    (yw - max(2, round(0.4 * staff_line_distance)));

  local_variable(numeric)(xv)
    (round(1.0 * staff_line_distance));
  local_variable(numeric)(yv)
    (3.5 * staff_line_distance);

  local_variable(numeric)(xi)
    (xv + max(2, 0.2 * staff_line_distance));
  local_variable(numeric)(yi)(yv);

  local_variable(numeric)(xx)
    (xg + max(2, round(0.3 * staff_line_distance)));
  local_variable(numeric)(yx)(3.5 * staff_line_distance);

  local_variable(numeric)(bigdot_diameter)
    (staff_line_distance);

  local_variable(numeric)(yo)
    (-staff_line_distance 
     - round(0.5 * staff_line_distance));

  local_variable(numeric)(xn)
    (round(0.5 * staff_line_distance));
  local_variable(numeric)(yn)
    (yo - 0.5 * bigdot_diameter);

  local_variable(numeric)(xo)
    (xn + 0.5 * bigdot_diameter);

  local_variable(numeric)(xp)(xn + bigdot_diameter);
  local_variable(numeric)(yp)(yn);

  local_variable(numeric)(xq)(xo);
  local_variable(numeric)(yq)(yo - bigdot_diameter);

  local_variable(numeric)(xs)
    (xp + max(1, floor(0.4 * staff_line_distance)));
  local_variable(numeric)(ys)(yp);

  local_variable(numeric)(xl)
    (xs + stem_thickness);
  local_variable(numeric)(yl)(ys);

  local_variable(numeric)(xm)
    (xp - 1 * staff_line_thickness);
  local_variable(numeric)(ym)
    (round(-2.75 * staff_line_distance));

  local_variable(numeric)(xr)
    (xm);
  local_variable(numeric)(yr)
    (ym + staff_line_thickness);

  local_variable(numeric)(xz)(xe);
  local_variable(numeric)(yz)
    (-staff_line_distance + staff_line_thickness);

  local_variable(numeric)(xaa)
    (xd - max(1, round(0.3 * staff_line_distance)));
  local_variable(numeric)(yaa)(yd);

  local_variable(numeric)(xbb)(xc);
  local_variable(numeric)(ybb)
    (staff_line_distance - staff_line_thickness 
     - max(2, 0.3 * staff_line_distance));

  local_variable(numeric)(xdd)(xp);
  local_variable(numeric)(ydd)(2 * staff_line_distance);

  local_variable(numeric)(xee)(xn);
  local_variable(numeric)(yee)(ydd);

  local_variable(numeric)(xff)
    (floor(1.4 * staff_line_distance));
  local_variable(numeric)(yff)
    (staff_line_distance);

  local_variable(numeric)(xgg)
    (xff + stem_thickness);
  local_variable(numeric)(ygg)
    (yff);

  local_variable(path)(stuff)
    ((xa, ya) .. (xb, yb){up} .. (xc, yc){right} ..
     (xd, yd){down} .. (xe, ye){left} .. (xf, yf){up} ..
     (xee, yee) ..
     (xg, yg){up} .. 
     tension 1 and 1.8 ..
     (xh, yh){left} .. 
     tension 1.8 and 1 ..
     (xi, yi) ..
     tension 1.8 and 1 ..
     (xgg, ygg){(1, -4)} ..
     tension 1 and 20 ..
     (xl, yl){down} .. 
     (xm, ym){left} ..
     (xn, yn){up} .. (xo, yo){right} .. (xp, yp){down} ..
     (xq, yq) & 
     (xq, yq) .. (xr, yr){right} ..
     (xs, ys){up} ..
     tension 20 and 1 ..
     (xff, yff){(-1, 4)} ..
     tension 1 and 1.8 ..
     (xv, yv){up} ..
     tension 1 and 1.8 ..
     (xw, yw){right} .. 
     tension 1.8 and 1 ..
     (xx, yx){down} ..
     (xdd, ydd) ..
     (xy, yy){down} .. (xz, yz){right} ..
     (xaa, yaa){up} .. (xbb, ybb){left} .. 
     (xcc, ycc){down} .. (xa + 1, ya) & 
     (xa + 1, ya) -- (xa, ya) .. cycle);

   begin_character(g_clef)

     fill reverse(stuff) shifted (0,staff_line_thickness) scaled magnification;

   end_character;
     
   begin_character(g_clef_light)

     fill reverse(stuff) shifted (0,staff_line_thickness) scaled magnification;
     stripes(3,6);

   end_character;
     
  tracingnone;

endgroup;
